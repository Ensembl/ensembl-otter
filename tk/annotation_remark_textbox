#!/usr/bin/env perl
# Copyright [2018-2024] EMBL-European Bioinformatics Institute
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use strict;
use warnings;

### annotation_remark_textbox

use Tk::ROText;
use CanvasWindow;

my $ann_tag = 'annotation';
my $voc_tag = 'controlled_vocabulary';
my $ctrl_vocab_menu;

{
    my $widget = CanvasWindow::MainWindow->new('Annotation Remarks');

    my $std_border = 3;
    my $frame = $widget->Frame(
        -border => $std_border,
        )->pack(-side => 'top');
    my $label_switch_frame = $frame->Frame(
        -border => $std_border,
        )->pack(
            -side => 'left',
            -expand => 1,
            -fill => 'y',
            );
    
    my @label_pack = (-side => 'top', -expand => 1, -fill => 'x');
    my @label_anchor = (-padx => $std_border, -anchor => 'w');
    my $text_label = $label_switch_frame->Label(
        -text   => "Remarks:",
        @label_anchor,
        )->pack(@label_pack);


    # Button for setting Visible/annotation remarks
    my @annotation_color = (-foreground => 'white', -background => 'IndianRed3');
    my $annotation_button = $label_switch_frame->Button(
        -text   => $ann_tag,
        @label_anchor,
        @annotation_color,
        -activeforeground => 'white',
        -activebackground => 'IndianRed2',
        )->pack(@label_pack);

    my $text = $frame->Scrolled('Text',
        -scrollbars         => 'e',
        -width              => 30,
        -height             => 4,
        -exportselection    => 1,
        -background         => 'white',
        -wrap               => 'word',
        );
    $text->pack(-side => 'left', -expand => 1, -fill => 'both');
    $text->tagConfigure($ann_tag, @annotation_color);
    $text->tagLower($ann_tag, 'sel');
    $text->tagConfigure($voc_tag,
            -foreground => 'black',
            -background => 'GreenYellow',
            );
    $text->tagLower($voc_tag, 'sel');

    my $tw = $text->Subwidget('text');
    my $class = ref($tw);

    # We need to ignore any sequences which edit text when inside
    # controlled vocabulary tagged text
    foreach my $seq (qw{

        <Button-2>
        <ButtonRelease-2>

        <<Cut>>
        <<Paste>>

        <Control-Key-t>

        <Return>
        <Control-Key-o>
        
        <Tab>
        <Control-Key-i>
        
        <F2>  <F3>

    }) {
        $tw->bind($seq, [\&ignore_in_controlled_vocab, Tk::Ev('K')]);
    }
    
    # Keyboard sequences which delete backwards need to take out the whole
    # line of controlled vocabulary in one go...
    foreach my $seq (qw{
        
        <BackSpace>
        <Control-Key-h>
        <Meta-Key-BackSpace>

    }) {
        $tw->bind($seq, [\&backspace_delete_whole_ctrl_vocab_line, Tk::Ev('K')]);
    }

    # ... as do sequences which delete forwards.
    foreach my $seq (qw{
        
        <Delete>
        <Meta-Key-d>

        <Control-Key-k>
        <Control-Key-d>

    }) {
        $tw->bind($seq, [\&forward_delete_whole_ctrl_vocab_line, Tk::Ev('K')]);
    }
    
    # Do not post the Text class's built in popup menu
    $tw->bind($class, '<Button-3>', '');
    $tw->bind('<Button-3>', [\&post_ctrl_vocab_menu, Tk::Ev('X'), Tk::Ev('Y')]);
    # $tw->bind('<ButtonRelease-3>', sub{ $ctrl_vocab_menu->unpost });

    $ctrl_vocab_menu = $tw->Menu(
        -tearoff    => 0,
        );
    foreach my $phrase (
        q{alternative_5'_UTR},
        q{readthrough},
        q{NMD exception},
        q{not organism-supported},
        q{not best-in-genome evidence},
        q{non-submitted evidence},
        q{for experimental confirmation},
    ) {
        $ctrl_vocab_menu->add('command',
            -label      => $phrase,
            -command    => sub {
                insert_phrase($tw, $phrase);
            },
        );
    }

    # Remove key binding for keyboard input and replace with our own which
    # inserts characters using the same tag as the rest of the line, or
    # which ignores characters with the controlled vocabulary tag.
    $tw->bind($class, '<Key>', '');
    $tw->bind('<Key>', [\&insert_char, Tk::Ev('A')]);


    my (@tags) = $tw->bindtags;
    warn "tags=(@tags)\n";
    $tw->bindtags([@tags[1, 0, 2, 3]]);

    # $tw->tagBind('annotation', '<BackSpace>', sub{ warn "BackSpace\n" });


    foreach my $sequence ($tw->Tk::bind($class)) {
        print "        $sequence\n";
        # if ($sequence =~ /Key/) {
        #     #print STDERR "seq=$sequence\n";
        #     $canvas->Tk::bind($class, $sequence, '');
        # }
    }

    $annotation_button->configure(-command => sub {
        my ($line) = $text->index('insert') =~ /^(\d+)/;
        my $line_start = "$line.0";
        my @this_line = ("$line_start", "$line_start lineend");
        #warn "line start = $line_start";
        my $annotation_is_set = 0;
        if (grep $_ eq $ann_tag, $text->tagNames("$line_start")) {
            $annotation_is_set = 1;
            $text->tagRemove($ann_tag, @this_line);
        }
        unless ($annotation_is_set) {
            $text->tagAdd($ann_tag, @this_line);
        }
    });
    
    my $button_frame = $widget->Frame->pack(-side => 'top');
    $button_frame->Button(
        -text => 'Get remarks',
        -command => sub {
                my %ann_index = $text->tagRanges($ann_tag);
                my $line = 0;
                foreach my $string (split /\n/, $text->get('1.0', 'end')) {
                    $line++;
                    # Trim trailing spaces and full-stops from remark
                    $string =~ s/[\s\.]+$//;
                    next if $string eq '';
                    my $type = $ann_index{"$line.0"} ? 'Annotation_remark' : 'Remark';
                    print STDERR "$type: '$string'\n";
                }
            },
        )->pack(-side => 'left');

    $text->insert('1.0', "Line 1\nLine second is a really long line that will wrap\nLine the third\n");
    $text->insert('2.0', "Controlled vocab", $voc_tag, "\n");

    Tk::MainLoop();
}

sub post_ctrl_vocab_menu {
    my ($text, $x, $y) = @_;
    
    $ctrl_vocab_menu->Post($x, $y);
}

sub insert_phrase {
    my ($text, $phrase) = @_;
    
    my @vocab_lines = $text->tagRanges($voc_tag);
    my $see_i;
    for (my $i = 0; $i < @vocab_lines; $i += 2) {
        my ($a, $b) = @vocab_lines[$i, $i + 1];
        my $text = $text->get($a, $b);
        if ($text eq $phrase) {
            $see_i = $a;
            last;
        }
    }
    unless ($see_i) {
        $see_i = '1.0';
        $text->insert($see_i, $phrase, $voc_tag, "\n", '');
    }
    $text->see($see_i);
}

sub backspace_delete_whole_ctrl_vocab_line {
    my ($text, $keysym) = @_;

    my $prev = $text->index('insert - 1 chars');

    if (is_ctrl_vocab_char($text, $prev)) {
        $text->delete("$prev linestart", "$prev lineend");
        $text->break;
    }
    elsif ($text->compare('insert', '==', 'insert linestart')) {
        # If this or the previous line is controlled vocab, just move the cursor
        if (is_ctrl_vocab_char($text, "$prev - 1 chars") or is_ctrl_vocab_char($text, 'insert')) {
            $text->SetCursor('insert - 1 chars');
            $text->break;
        }
    }
}

sub forward_delete_whole_ctrl_vocab_line {
    my ($text, $keysym) = @_;

    if (is_ctrl_vocab_char($text, 'insert')) {
        $text->delete('insert linestart', 'insert lineend');
        $text->break;
    }
    elsif ($text->compare('insert', '==', 'insert lineend')) {
        # If this or the next line is controlled vocab, just move the cursor
        if (is_ctrl_vocab_char($text, 'insert + 1 chars') or is_ctrl_vocab_char($text, 'insert - 1 chars')) {
            $text->SetCursor('insert + 1 chars');
            $text->break;
        }
    }
}

sub ignore_in_controlled_vocab {
    my ($text, $keysym) = @_;
    
    # Need to choose "insert" for keyboard events
    # and "current" for mouse events.
    my $posn = $keysym ? 'insert' : 'current';

    if ($text->compare($posn, '==', "$posn linestart")) {
        # Return at linestart is always OK
        return if $keysym eq 'Return';
    }
    
    if (is_ctrl_vocab_char($text, $posn)) {
        $text->break;
    }
}

sub is_ctrl_vocab_char {
    my ($text, $posn) = @_;
    
    return grep $_ eq $voc_tag, $text->tagNames($posn);
}

# Inserts (printing) characters with the same style as the rest of the line
sub insert_char {
    my( $text, $char ) = @_;
    
    # We only want to insert printing characters in the Text box!
    # [:print:] is the POSIX class of printing characters.
    return unless $char =~ /[[:print:]]/;

    # Do not edit controlled vocabulary
    return if grep $_ eq $voc_tag, $text->tagNames('insert linestart');

    # Expected behaviour is that any selected text will
    # be replaced by what the user types.
    $text->deleteSelected;
    
    # There will only ever be one or zero tags per line in out Text box.
    my ($tag) = grep $_ eq $ann_tag, $text->tagNames('insert linestart');
    
    $text->insert('insert', $char, $tag);
}

__END__

=head1 NAME - annotation_remark_textbox

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

