#!/usr/bin/env perl
# Copyright [2018-2022] EMBL-European Bioinformatics Institute
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use strict;
use warnings;


### hand_scroll

use CanvasWindow;

{
    my $x = 1000;
    my $y = 800;

    my $mw = CanvasWindow::MainWindow->new;
    $mw->title('Click and drag to scroll canvas');
    
    my $cw = CanvasWindow->new($mw, $x / 3, $y / 3);
    
    my $canvas = $cw->canvas;
    
    # This implements scroll dragging of the canvas using
    # the left mouse button. May need to use other mouse buttons
    # in applications where button presses and releases are bound
    # to other actions.
    $canvas->Tk::bind('<ButtonPress-1>',    [ \&save_current, Tk::Ev('x'), Tk::Ev('y') ] );
    $canvas->Tk::bind('<Button1-Motion>',   [ \&hand_scroll,  Tk::Ev('x'), Tk::Ev('y') ] );
    $canvas->Tk::bind('<ButtonRelease-1>',  \&restore_cursor);

    # Draw some squares so that we can see the effect of dragging
    for (1..200) {
        my $x1 = rand($x);
        my $y1 = rand($y);
        my $x2 = $x1 + 10;
        my $y2 = $y1 + 10;
        $canvas->createRectangle($x1,$y1, $x2,$y2,
            -fill       => 'salmon',
            -outline    => 'black',
            );
    }
    
    $cw->set_scroll_region;
    Tk::MainLoop();  
}

sub save_current {
    my( $canvas, $x, $y ) = @_;

    # The "fleur" cursor actually appears
    # as a hand on many systems.
    $canvas->configure(-cursor => 'fleur');
    $canvas->scanMark($x, $y);
}

sub hand_scroll {
    my( $canvas, $x, $y ) = @_;

    # Third argument is "gain", which defaults to 10!
    $canvas->scanDragto($x, $y, 1);
}

sub restore_cursor {
    my ($canvas) = @_;

    # Sets the cursor back to its default
    $canvas->configure(-cursor => undef);
}

__END__

=head1 NAME - hand_scroll

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

