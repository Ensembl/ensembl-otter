#!/usr/bin/env perl
# Copyright [2018-2019] EMBL-European Bioinformatics Institute
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


### remove_transcripts

use strict;
use warnings;

use Try::Tiny;

use Bio::Otter::Lace::Defaults;
use Bio::Vega::DBSQL::DBAdaptor;
use Bio::Vega::SliceLockBroker;
use Sys::Hostname 'hostname';

sub main {
    my $dsname;

    Bio::Otter::Lace::Defaults::do_getopt(
        'dataset=s'     => \$dsname,
    );
    
    unless ($dsname) {
        Bio::Otter::Lace::Defaults::show_help();
    }

    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    my $ds = $cl->get_DataSet_by_name($dsname);
    my $dba = $ds->get_cached_DBAdaptor;
    my $gene_dba = $dba->get_GeneAdaptor;

    my $author = Bio::Vega::Author->new(
        -NAME       => $cl->author,
        -EMAIL      => $cl->email,
        );

    my $exit = 0;
    while (<>) {
        foreach my $t_stable_id (split) {
            $exit |= delete_transcript($t_stable_id, $gene_dba, $author);
        }
    }

    return $exit;
}

sub delete_transcript {
    my ($t_stable_id, $gene_dba, $author) = @_;
    
    my $gene = $gene_dba->fetch_by_transcript_stable_id($t_stable_id);
    unless ($gene) {
        warn "Can't fetch gene for transcript '$t_stable_id'\n";
        return;
    }

    # Need to make a fresh transcript array because the old one
    # is cached inside the TranscriptAdaptor.
    my $tsct_list = [ @{$gene->get_all_Transcripts} ];
    $gene->{'_transcript_array'} = $tsct_list;

    printf STDERR "%s in gene with %d transcripts\n", $t_stable_id, scalar @$tsct_list;
    my $found = 0;
    for (my $i = 0; $i < @$tsct_list; $i++) {
        my $this = $tsct_list->[$i];
        if ($this->stable_id eq $t_stable_id) {
            splice(@$tsct_list, $i, 1);
            $found = 1;
            last;
        }
    }
    unless ($found) {
        die "Failed to find transcript '$t_stable_id' in gene";
    }

    unless ($gene->iscurrent) {
        my $gsid = $gene->stable_id;
        my $dbid = $gene->dbID;
        die "Gene $gsid $dbid for $t_stable_id is not current";
    }

    my $broker = Bio::Vega::SliceLockBroker->new
      (-hostname => hostname(), -author => $author, -adaptor => $gene_dba);

    my $work = sub {
        if (@$tsct_list) {
            $gene_dba->store($gene);
        } else {
            $gene_dba->remove($gene);
        }
        return;
    };

    my $retcode = try {
        $broker->lock_create_for_objects(remove_transcripts => $gene);
        $broker->exclusive_work($work, 1);
        0;
    } catch {
        warn "$t_stable_id: $_";
        2;
    } finally {
        $broker->unlock_all;
    };

    return $retcode;
}

exit main();


__END__

=head1 NAME - remove_transcripts

=head1 USAGE

  remove_transcripts -dataset <DSNAME>

=head1 DESCRIPTION

Removes all the transcripts given by a list of transcript stable IDs supplied
via ARGV. A new version of each genes is saved, or the gene is removed if all
of its transcripts are removed.

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

