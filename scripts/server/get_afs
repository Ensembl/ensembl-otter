#!/usr/local/bin/perl5.8.0 -w

my $enshead = $ENV{ENSHEAD}; # is set by the server for any GET request

use strict;
use OtterDefs;
use CGI;
use Bio::Otter::ServerSide (':all');
use Bio::Otter::Lace::PipelineDB;
use Bio::EnsEMBL::DBSQL::DBAdaptor;
use Bio::Otter::DBSQL::DBAdaptor;
use Bio::Otter::Lace::ViaText ('%OrderOfOptions');
use Bio::Otter::DBSQL::SimpleBindingAdaptor;
use Bio::Otter::HitDescription;

$| = 1;

my $cgi = new CGI;
set_nph($cgi);
my %cgi_args = $cgi->Vars;

my $odb = get_DBAdaptor_from_CGI_species($cgi, $OTTER_SPECIES, $enshead);
my ($pdb, $pipeline_slice, $pipe_connparms) = get_pipeline_adaptor_slice_parms($cgi, $odb, $enshead);

my @af_optnames = @{ $OrderOfOptions{AlignFeature} };
my @hd_optnames = @{ $OrderOfOptions{HitDescription} };

my $analysis = $cgi_args{analysis};
my $kind     = $cgi_args{kind};
my $method = {
    'dafs' => 'get_DnaAlignFeatureAdaptor',
    'pafs' => 'get_ProteinAlignFeatureAdaptor',
}->{$kind};

    # Fetch the features:
my $af_adaptor = $pdb->$method();
my $afs = $af_adaptor->fetch_all_by_Slice($pipeline_slice, $analysis);

print STDERR "Total of ".scalar(@$afs)." $kind:$analysis features found\n";

    # Put the names into the hit_description hash:
my %hd_hash = ();
foreach my $af (@$afs) {
    $hd_hash{$af->hseqname()} = '';
}

    # Fetch the hit descriptions:
my $hd_adaptor = Bio::Otter::DBSQL::SimpleBindingAdaptor->new( %{$pipe_connparms} );
$hd_adaptor->fetch_into_hash(
    'hit_description',
    'hit_name',
    { qw(
        hit_length _hit_length
        hit_description _description
        hit_taxon _taxon_id
        hit_db _db_name
    )},
    'Bio::Otter::HitDescription',
    \%hd_hash,
);

    # Stringify only the simple fields:
my %hit_seen = (); # collect the seen hit names here
foreach my $af (@$afs) {
    my $hit_name = $af->hseqname();
    my $hd = $hd_hash{$hit_name};
    if($hd) {
        if(!exists($hit_seen{$hit_name})) { # a new one

                # output a HitDescription line
            my @hd_optvalues = ('HitDescription', $hit_name);
            for my $opt (@hd_optnames) {
                push @hd_optvalues, $hd->$opt();
            }
            print join("\t", @hd_optvalues)."\n";

            $hit_seen{$hit_name} = 1;
        }
    }
    
        # output an AlignFeature line
    my @af_optvalues = ('AlignFeature');
    for my $opt (@af_optnames) {
        push @af_optvalues, $af->$opt();
    }
    push @af_optvalues, $af->cigar_string();

    print join("\t", @af_optvalues)."\n";
}

