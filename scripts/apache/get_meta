#!/usr/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg

use strict;
use warnings;

BEGIN { use lib ($ENV{OTTER_PERL_INC} || q{}) =~ m{([^:]+)}g }
use SangerPaths qw{ core bioperl123 ensembl71 otter75 };

use Bio::Otter::ServerScriptSupport;

my $select_meta_sql = <<'SQL'
    SELECT species_id, meta_key, meta_value
      FROM meta
  ORDER BY meta_id
SQL
    ;

my @white_list = qw(
    assembly
    patch
    prefix
    schema_type
    schema_version
    species
);

sub get_meta {
    my ($server) = @_;

    my %white_list = map { $_ => 1 } @white_list;

    my $sth = $server->otter_dba()->dbc()->prepare($select_meta_sql);
    $sth->execute;

    my $counter = 0;
    my $output_string ='';
    while (my ($species_id, $meta_key, $meta_value) = $sth->fetchrow) {

        my ($key_prefix) = $meta_key =~ /^(\w+)\.?/;
        next unless $white_list{$key_prefix};

        $species_id = '' unless defined $species_id;
        $meta_value=~s/\s+/ /g; # get rid of newlines and tabs

        $output_string .= join("\t", $meta_key, $meta_value, $species_id)."\n";
        $counter++;
    }

    warn "Total of $counter meta table pairs sent\n";

    return $server->otter_wrap_response($output_string);
}

Bio::Otter::ServerScriptSupport->send_response(\&get_meta);
