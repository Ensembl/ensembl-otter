#!/usr/local/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg
# Last modified: $Date: 2007-10-26 11:35:59 $
# Id:            $Id: nph-get_genes,v 1.7 2007-10-26 11:35:59 lg4 Exp $
# Source:        $Source: /tmp/ENSCOPY-ENSEMBL-OTTER/scripts/apache/Attic/nph-get_genes,v $

use strict;
use SangerPaths qw{ core bioperl123 ensembl46 otter48 };

use Bio::Otter::ServerScriptSupport;
use Bio::Otter::ToXML;

sub Bio::EnsEMBL::Gene::propagate_slice {
    my ($gene, $slice) = @_;

    foreach my $transcript (@{ $gene->get_all_Transcripts() }) {
        foreach my $exon (@{ $transcript->get_all_Exons() }) {
            $exon->slice($slice);
        }
        $transcript->slice($slice);
    }
    $gene->slice($slice);
}

my $server = Bio::Otter::ServerScriptSupport->new;

my $analysis             = $server->param('analysis');
my $transcript_analyses  = $server->param('transcript_analyses')  || '';
my $translation_xref_dbs = $server->param('translation_xref_dbs') || '';

my $output_string = '';
eval {
    my $allowed_transcript_analyses_hash = $transcript_analyses
        ? { map { $_ => 1 } split(/,/, $transcript_analyses) }
        : '';
    my $allowed_translation_xref_db_hash = $translation_xref_dbs
        ? { map { $_ => 1 } split(/,/, $translation_xref_dbs) }
        : '';

        # third parameter of $slice->get_all_Genes() helps preventing lazy-loading of transcripts
    my $genes = $server->fetch_mapped_features('gene', 'get_all_Genes', [$analysis, undef, 1],
        map { defined($server->param($_)) ? $server->param($_) : '' } qw(cs name type start end strand metakey csver csver_remote)
    );

    foreach my $gene (@$genes) {
        $output_string .= $gene->toXMLstring(
                                        $allowed_transcript_analyses_hash,
                                        $allowed_translation_xref_db_hash
                                    );
    }
};

if ($@) {
    $server->error_exit("Error fetching genes:\n" . $@);
} else {
    $server->send_response($output_string, 1);
}

1;

