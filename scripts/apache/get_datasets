#!/usr/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg

use strict;
use warnings;

BEGIN { use lib ($ENV{OTTER_PERL_INC} || q{}) =~ m{([^:]+)}g }
use SangerPaths qw{ core bioperl123 ensembl72 otter76 };

use Bio::Otter::ServerScriptSupport;

# For added security we don't show mysql connection details to clients outside
# the Sanger. We only need connection details to for scripts running at Sanger.
my %secret_params = map { ( $_ => 1 ) } qw{
    HOST
    PORT
    USER
    PASS
    DBNAME

    DNA_HOST
    DNA_PORT
    DNA_USER
    DNA_PASS
    DNA_DBNAME
};

sub get_datasets {
    my ($server) = @_;

    my $xml = '';

    # Only local users get to see mysql connection details
    my $show_details = $server->local_user;

    $xml   .= qq{  <datasets>\n};
    foreach my $dataset (@{$server->allowed_datasets}) {
        my $name   = $dataset->name;
        my $params = $dataset->params;

        $xml .= qq{    <dataset name="$name">\n};

        foreach my $key (keys %$params) {
            unless ($show_details) {
                next if $secret_params{$key};
            }
            my $lckey = lc($key);   ### Why do we bother lower casing this?
            $xml .= qq{      <$lckey>$params->{$key}</$lckey>\n};
        }

        $xml .= qq{    </dataset>\n};
    }
    $xml .= qq{  </datasets>\n};

    return $server->otter_wrap_response($xml);
}

Bio::Otter::ServerScriptSupport->send_response(\&get_datasets);

__END__

=head1 NAME - get_datasets

=head1 DESCRIPTION

Returns a list of datasets to the Client. Returns a list of datasets which the
user is permitted to access (determined by entries in the users.txt file for
external users and restricted datasets).

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

