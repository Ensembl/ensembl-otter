#!/usr/bin/perl -Tw

use strict;
use warnings;

BEGIN { use lib ($ENV{OTTER_PERL_INC} || q{}) =~ m{([^:]+)}g }
use SangerPaths qw{ core bioperl123 otter };
use Bio::Otter::Git qw{ :server_ensembl :match };

use Bio::Otter::Server::Support::Web;

use Bio::Vega::SliceLockBroker;


sub get_SliceLocks {
    my ($server) = @_;

    my $SLdba = $server->otter_dba->get_SliceLockAdaptor;
    my $S_dba = $server->otter_dba->get_SliceAdaptor;
    my $chr = $server->require_argument('chr');
    my $coord_system_name = $server->require_argument('coord_system_name') || 'chromosome';
    my $coord_system_version = $server->require_argument('coord_system_version') || 'Otter';
    my $chr_slice = $S_dba->fetch_by_region
      ($coord_system_name, $chr, undef, undef, undef, $coord_system_version, 1);
    die "Cannot find $coord_system_name:$coord_system_version:$chr" unless $chr_slice;
    my $srid = $chr_slice->get_seq_region_id;

    my $slocks = $SLdba->fetch_by_seq_region_id($srid, 1); # extant = not freed
    @$slocks = grep { $_->active eq 'held' } @$slocks; # exclude 'pre'

    return $slocks;
}

sub get_locks {
    my ($server) = @_;
    my $ds = $server->dataset;
    my %out;

    $out{SliceLock} = get_SliceLocks($server)
      if Bio::Vega::SliceLockBroker->supported($ds);
    $server->json->convert_blessed;

    return \%out;
}

Bio::Otter::Server::Support::Web->send_response(-content_type => 'application/json', \&get_locks);
