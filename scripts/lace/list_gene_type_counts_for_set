#!/usr/local/bin/perl -w

### list_gene_type_counts_for_set

use strict;
use Bio::Otter::Lace::Defaults;

{
    my( $dataset_name, $set_name );
    my $usage = sub { exec('perldoc', $0) };
    Bio::Otter::Lace::Defaults::do_getopt(
        'dataset=s'     => \$dataset_name,
        'set|chr=s'     => \$set_name,
        ) or $usage->();
    $usage->() unless $dataset_name and $set_name;

    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    my $ds = $cl->get_DataSet_by_name($dataset_name);
    my $dba = $ds->get_cached_DBAdaptor;
    my $sth = $dba->prepare(q{
        SELECT gsid.stable_id
          , g.type
        FROM gene_stable_id gsid
          , gene g
          , transcript t
          , exon_transcript et
          , exon e
          , assembly a
        WHERE gsid.gene_id = g.gene_id
          AND g.gene_id = t.gene_id
          AND t.transcript_id = et.transcript_id
          AND et.exon_id = e.exon_id
          AND e.contig_id = a.contig_id
          AND a.type = ?
        GROUP BY gsid.stable_id
          , gsid.version
        ORDER BY gsid.version ASC
        });
    $sth->execute($set_name);
    
    my( %sid_type );
    while (my ($sid, $type) = $sth->fetchrow) {
        $sid_type{$sid} = $type;
    }
    
    my( %type_count );
    foreach my $type (values %sid_type) {
        next if $type eq 'obsolete';
        $type_count{$type}++;
    }
    
    my $total = 0;
    print "\nGene types in $dataset_name $set_name:\n";
    foreach my $type (sort keys %type_count) {
        my $count = $type_count{$type};
        printf "%25s  %5d\n", $type, $count;
        $total += $count;
    }
    printf "\n%25s  %5d\n", 'Total:', $total;
}

__END__

=head1 NAME - list_gene_type_counts_for_set

=head1 EXAMPLE

  list_gene_type_counts_for_set -dataset pig -set mhc-01

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

