#!/usr/bin/env perl
# Copyright [2018-2022] EMBL-European Bioinformatics Institute
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use warnings;


### list_gene_type_counts_for_set

use strict;
use Bio::Otter::Lace::Defaults;

{
    my( $dataset_name, $set_name );
    my $usage = sub { exec('perldoc', $0) };
    Bio::Otter::Lace::Defaults::do_getopt(
        'dataset=s'     => \$dataset_name,
        'set|chr=s'     => \$set_name,
        ) or $usage->();
    $usage->() unless $dataset_name and $set_name;

    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    my $ds = $cl->get_DataSet_by_name($dataset_name);
    my $dba = $ds->get_cached_DBAdaptor;
    my $sth = $dba->prepare(q{
        SELECT gsid.stable_id
          , g.type
        FROM gene_stable_id gsid
          , gene g
          , transcript t
          , exon_transcript et
          , exon e
          , assembly a
        WHERE gsid.gene_id = g.gene_id
          AND g.gene_id = t.gene_id
          AND t.transcript_id = et.transcript_id
          AND et.exon_id = e.exon_id
          AND e.contig_id = a.contig_id
          AND a.type = ?
        GROUP BY gsid.stable_id
          , gsid.version
        ORDER BY gsid.version ASC
        });
    $sth->execute($set_name);
    
    my( %sid_type );
    while (my ($sid, $type) = $sth->fetchrow) {
        $sid_type{$sid} = $type;
    }
    
    my( %type_count );
    foreach my $type (values %sid_type) {
        next if $type eq 'obsolete';
        $type_count{$type}++;
    }
    
    my $total = 0;
    print "\nGene types in $dataset_name $set_name:\n";
    
    my @pre_type;
    foreach my $type (keys %type_count) {
        my ($pre, $root) = $type =~ /(?:(.+):)?(.+)/;
        push @pre_type, [$pre || ' ', $root, $type];
    }
    
    foreach my $prt (sort {$a->[1] cmp $b->[1] || $a->[0] cmp $b->[0]} @pre_type) {
        my $type = $prt->[2];
        my $count = $type_count{$type};
        printf "%25s  %5d\n", $type, $count;
        $total += $count;
    }
    printf "\n%25s  %5d\n", 'Total:', $total;
}

__END__

=head1 NAME - list_gene_type_counts_for_set

=head1 EXAMPLE

  list_gene_type_counts_for_set -dataset pig -set mhc-01

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

