#!/usr/bin/env perl
# Copyright [2018-2024] EMBL-European Bioinformatics Institute
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use warnings;


### fetch_clone_EMBL

use strict;

use Bio::Otter::Lace::Defaults;

{
    my $dataset_name = undef;
    my $sequence_set = undef;
    
    my $usage = sub { exec('perldoc', $0) };
    Bio::Otter::Lace::Defaults::do_getopt(
        'h|help!'   => $usage,
        'dataset=s' => \$dataset_name,
        'chr|set=s' => \$sequence_set,
        ) or $usage->();
    $usage->() unless $dataset_name and $sequence_set;

    # Connect to Ensembl/Otter database and fetch adaptors
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    my $ds = $cl->get_DataSet_by_name($dataset_name);
    my $otter_db = $ds->make_Vega_DBAdaptor;
    my $slice_aptr = $otter_db->get_SliceAdaptor;
    my $gene_aptr  = $otter_db->get_GeneAdaptor;

    foreach my $acc (@ARGV) {
        
        foreach my $chr_s_e (fetch_chr_start_end_for_accession($otter_db, $acc)) {
            my $slice = $slice_aptr->fetch_by_chr_start_end(@$chr_s_e);
            my $gene_id_list = $gene_aptr->list_current_dbIDs_for_Slice($slice);
            my $tile_path = $slice->get_tiling_path;
            if (@$tile_path != 1) {
                my $count = @$tile_path;
                die "Expected 1 component in tiling_path but have $count\n";
            }

            my $slice_contig = $tile_path->[0]->component_Seq;
            my $contig_length = $slice_contig->length;
            foreach my $gid (@$gene_id_list) {
                # Fetch gene
                my $gene = $gene_aptr->fetch_by_dbID($gid);
                
                # Deleted genes have type 'obsolete'
                next if $gene->type eq 'obsolete';
                
                foreach my $transcript (@{$gene->get_all_Transcripts}) {
                    my $sid = $transcript->stable_id;
                    print "\nTranscript $sid\n";
                    foreach my $exon (@{$transcript->get_all_Exons}) {
                        my $contig  = $exon->contig;
                        my $start   = $exon->start;
                        my $end     = $exon->end;
                        # May be an is_sticky method?
                        if ($exon->isa('Bio::Ensembl::StickyExon')) {
                            # Deal with sticy exon
                            warn "STICKY!\n";
                        }
                        elsif ($contig != $slice_contig) {
                            my $acc = $contig->clone->embl_id;
                            my $sv  = $contig->clone->embl_version;
                            # Is not on Slice
                            print "$acc.$sv:$start..$end\n";
                        }
                        else {
                            # Is on Slice (ie: clone)
                            if ($end < 1 or $start > $contig_length) {
                                warn "Unexpected exon start '$start' end '$end' on contig of length '$contig_length'\n";
                            }
                            print "$start..$end\n";
                        }
                    }
                }
            }
        }
    }
}

sub fetch_chr_start_end_for_accession {
    my( $db, $acc ) = @_;
    
    my $type = $db->assembly_type;
    my $sth = $db->prepare(q{
        SELECT chr.name
          , a.chr_start
          , a.chr_end
        FROM assembly a
          , contig c
          , clone cl
          , chromosome chr
        WHERE c.clone_id = cl.clone_id
          AND c.contig_id = a.contig_id
          AND chr.chromosome_id = a.chromosome_id
          AND cl.embl_acc = ?
          AND a.type = ?
        ORDER BY a.chr_start
        });
    $sth->execute($acc, $type);
    
    my( @chr_start_end );
    while (my ($chr, $start, $end) = $sth->fetchrow) {
        push(@chr_start_end, [$chr, $start, $end]);
    }
    if (@chr_start_end) {
        return @chr_start_end;
    } else {
        die "Clone with accession '$acc' not found on assembly '$type'\n";
    }
}

__END__

=head1 NAME - fetch_clone_EMBL

=head1 EXAMPLE

  fetch_clone_EMBL -dataset human -chr chr1-07 AC114498 AL669831

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

