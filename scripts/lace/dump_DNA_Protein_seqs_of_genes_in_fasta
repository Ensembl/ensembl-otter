#!/usr/bin/env perl
# Copyright [2018] EMBL-European Bioinformatics Institute
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#      http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use warnings;


### dump_DNA_Protein_seqs_of_genes_in_fasta

use strict;
use Getopt::Long 'GetOptions';
use Bio::Otter::Lace::Defaults;

$| = 1;

my ($dataset, @sets, $vega, $all_exons, $cds_only, $filter, $type);

$all_exons = 1; # default

my $help = sub { exec('perldoc', $0) };

Bio::Otter::Lace::Defaults::do_getopt(
    'ds|dataset=s' => \$dataset, # eg, human or mouse or zebrafish
    's|set=s@'     => \@sets,    # sequence set(s) to check
    'all-exons'    => \$all_exons,
    'cds-only'     => \$cds_only,
    'filter'       => \$filter,
    'h|help'       => $help,
    'type=s'       => \$type,
    'v|vega'       => \$vega,    # flags vega_set_id > 0. !$vega: check all datasets
    # for now, zebrafish defaults to !$vega
    ) or $help->();               # plus default options
$help->() unless ( $dataset && $type );
$help->() if ( $vega and @sets );                                  # vega option does all vega sets

my $client   = Bio::Otter::Lace::Defaults::make_Client();          # Bio::Otter::Lace::Client
my $dset     = $client->get_DataSet_by_name($dataset);             # Bio::Otter::Lace::DataSet
my $otter_db = $dset->get_cached_DBAdaptor;                        # Bio::EnsEMBL::Containerr
my $sliceAd  = $otter_db->get_SliceAdaptor;
my $geneAd   = $otter_db->get_GeneAdaptor;                         # Bio::Otter::AnnotatedGeneAdaptor

my $date = `date +%Y%m%d`; chomp $date;
my $gene_info_of_trans    = {};
my $gene_info_of_trans_AA = {};
my $stblID_acc;

# loop thru all assembly types to fetch all annotated genes in it on otter
if ( @sets ) {

  foreach my $set ( @sets ) {

    $stblID_acc = gene_stable_id_2_acc_sv($set) if $filter;

    my $seqSet    = $dset->get_SequenceSet_by_name($set);
    $dset->fetch_all_CloneSequences_for_SequenceSet($seqSet);

    print STDERR "\nExamining Transcripts on '$set'\n\n";
    printf STDERR ("%s\n\t%s\n\t%s\n\t%s\n\t\n",
                   "Transcript legend:", "e: external", "x: excluded", ".: wanted");

    my $chrom;
    $chrom = $seqSet->CloneSequence_list()->[0]->chromosome;

    my $slice   = $sliceAd->fetch_by_chr_name($chrom->name);

    my $latest_gene_ids = $geneAd->list_current_dbIDs_for_Slice($slice);

    output_sequences( get_latest_trans_or_prot_seq($latest_gene_ids, $slice) );
  }
}


#-----------------------------------------------------------
#                   s u b r o u t i n e s
#-----------------------------------------------------------


sub get_latest_trans_or_prot_seq {

  my ($latest_gene_id, $slice) = @_;
  my $transClass_trans = {};
  my $geneType_trans   = {};
  my $counter;

  foreach my $id ( @$latest_gene_id ) {

    # transform contig coords of a gene into chrom. coords
    my $gene;
    if ($slice) {
        $gene = $geneAd->fetch_by_dbID($id)->transform($slice);
    } else {
        $gene = $geneAd->fetch_by_dbID($id);
    }

    # always filter out gene type = "obsolete"
    next if $gene->type eq "obsolete";

    my $trans_list = $gene->get_all_Transcripts;

    # group transcripts into coding, Pseudogene and the rest;

    foreach my $trans ( @$trans_list ) {

      my $geneNAME   = $gene->gene_info->name->name;


      # don't want these external ones (which begin with a "PREFIX:")

      if ( $geneNAME =~ /^[\w+]:/ ){
         $counter++;
         print STDOUT "e";
         print STDOUT "\t$counter\n"  if $counter % 100 == 0;
         next;
      }

      # take only a fraction of clones the annotators want
      my $geneID     = $gene->stable_id;

      if ( $filter ){
        unless ( `grep $stblID_acc->{$geneID} /nfs/team71/analysis/ck1/SCRATCH/chr21-02_clones` ){
          $counter++;
          print STDOUT "x";
          print STDOUT "\t$counter\n"  if $counter % 100 == 0;
          next;
        }
      }

      my $geneTYPE   = $gene->type;
      my $geneDESC;
      $gene->description ? $geneDESC = $gene->description : $geneDESC = "None";

      my $transID    = $trans->transcript_info->transcript_stable_id;
      my $transNAME  = $trans->transcript_info->name;
#      my $transCLASS = $trans->type; # this returns gene_type
      my $transCLASS = $trans->transcript_info->class->name;
      my $transSTART = $trans->start;
      my $transEND   = $trans->end;

      $counter++;
      print STDOUT ".";
      print STDOUT "\t$counter\n"  if $counter % 100 == 0;

      if ( $trans->translation ){

        # take care of lineal gene order

        my $protID  = $trans->translation->stable_id;

        push(@{$gene_info_of_trans_AA->{$trans}}, "$transID ($transSTART - $transEND) | GENE_ID $geneID | GENE_TYPE $geneTYPE | SYMBOL $geneNAME | TRANS_NAME $transNAME | TRANS_CLASS $transCLASS | PROT_ID $protID | DESC $geneDESC");
      }

      push(@{$gene_info_of_trans->{$trans}}, "$transID ($transSTART - $transEND) | GENE_ID $geneID | GENE_TYPE $geneTYPE | SYMBOL $geneNAME | TRANS_NAME $transNAME | TRANS_CLASS $transCLASS | DESC $geneDESC");

      next if $trans->transcript_info->class->name eq "Transposon";

      # dumping categories based on transcript type or gene type

      if ( $type eq "trans" ){
    #    print $type,"***\n";
        push( @{$transClass_trans->{$transCLASS}->{$transSTART}}, $trans);
      }
      elsif ( $type eq "gene" ) {
        push( @{$geneType_trans->{$geneTYPE}->{$transSTART}}, $trans ); # take care of linear gene order
      }
    }
  }
  print STDOUT "\t$counter\n";


  return $geneType_trans   if $type eq "gene";
  return $transClass_trans if $type eq "trans";

  return;
}

sub output_sequences {

  my ($obj_sets) = shift;
  my ($protein, $fh);

  # $type_or_class: transcript_class.name or gene.type in otter db

  foreach my $type_or_class ( keys %{$obj_sets} ) {

    # take care of linear gene order
    my @chr_coord_order = sort {$a <=> $b} keys %{$obj_sets->{$type_or_class}};

    foreach my $start_coord ( @chr_coord_order ) {

      foreach my $t ( @{$obj_sets->{$type_or_class}->{$start_coord}} ) {

        # get seq. from all exons or only from CDS (no UTRs)
        my $formatted_seq_all_exons;
        my $formatted_seq_CDS_only;

        if ( $all_exons ) {
          my $exon_seqs;
          foreach my $exon ( @{$t->get_all_Exons} ) {
            $exon_seqs .= $exon->seq->seq;
          }
          $formatted_seq_all_exons = lc(sixty_cols($exon_seqs));
        }

        if ( $cds_only ) {
          my $exon_seqs;
          foreach my $exon ( @{$t->get_all_translateable_Exons} ) {
            $exon_seqs .= $exon->seq->seq;
          }
          $formatted_seq_CDS_only = lc(sixty_cols($exon_seqs));
        }

        my ($filename_a, $filename_c);

        if ( $type_or_class =~ /pseudogene/i ) {
          $filename_a = "Pseudogenes_DNA_".$date;
          $filename_c = "Pseudogenes_DNA_CDS_only_".$date;
        } else {
          $filename_a = $type_or_class."_DNA_".$date;
          $filename_c = $type_or_class."_DNA_CDS_only_".$date;
        }

        if ( $all_exons ) {
          open($fh, '>>', $filename_a) or die $!;
          print $fh ">@{$gene_info_of_trans->{$t}}\n"; # fasta header
          print $fh $formatted_seq_all_exons, "\n";
        }
        if ( $cds_only ) {
          open($fh, '>>', $filename_c) or die $!;
          print $fh ">@{$gene_info_of_trans->{$t}}\n";
          print $fh $formatted_seq_CDS_only, "\n"  if $cds_only;
        }

        if ( $type_or_class eq "Coding"           or
             $type_or_class eq "Novel_CDS"        or
             $type_or_class eq "Known"            or
             $type_or_class eq "Novel_transcript" or
             $type_or_class eq "Predicted"        or
             $type_or_class eq "Putative"
           ) {

          # print also prtein seq to a separate file for conding genes

          my $filename_p = $type_or_class."_AA_".$date;
          open($fh, '>>', $filename_p) or die $!;

          # looks like some genes can be dubious due to incomplete annotation
          # eg, coding gene but its transcript does not have translation

          if ( exists $gene_info_of_trans_AA->{$t} ) {
            print $fh ">@{$gene_info_of_trans_AA->{$t}}\n";
            my $protein = sixty_cols($t->translate->primary_seq->seq);
            print $fh $protein, "\n";
          } else {
            my $gene = $geneAd->fetch_by_transcript_id($t->dbID);
        #    printf STDERR ("%s (%s) %s", $t->transcript_info->transcript_stable_id,
#                           $gene->stable_id, " has no associated translation\n");
          }
        }
      }
    }
  }

  return;
}

sub sixty_cols {

  my $seq  = shift;
  my $format_seq;

  while ($seq =~ /(.{1,60})/g) {
    $format_seq .= $1 . "\n";
  }
  return $format_seq;
}

sub gene_stable_id_2_acc_sv {

  # this subroutine is for filtering out unwanted clones specified by annotators
  # called only when $filter is true

  my $set = shift;
  my $sql = $otter_db->prepare(qq{
    SELECT distinct gsi.stable_id, c.name
    FROM contig c, assembly a, exon e, exon_transcript et,
         transcript t, gene g, gene_stable_id gsi
    WHERE a.type='$set'
    AND a.contig_id = c.contig_id
    AND c.contig_id = e.contig_id
    AND e.exon_id = et.exon_id
    AND et.transcript_id = t.transcript_id
    AND t.gene_id = g.gene_id
    AND g.gene_id = gsi.gene_id
    }
      );

  $sql->execute;

  my $stblID_acc = {};
  while ( my ($stblID, $contigName) = $sql->fetchrow ){
    $contigName =~ /(.+\.\d+)\.\d+\.\d+/;
    $stblID_acc->{$stblID} = $1;
  }

  return $stblID_acc;
}

__END__

=head1 NAME - dump_DNA_Protein_seqs_of_genes_in_fasta

=head1 SYNOPSIS

dump_DNA_Protein_seqs_of_genes_in_fasta -dataset <dataset> -set <assembly_type> <-all-exons and/or -cds-only> <-filter> -type gene


=head1 DESCRIPTION

Output DNA/Protein sequence of each splice variant of a gene in fasta format.

One of the -all-exons, -cds-only option is mandatory, default is -all-exons

-filter: useful if only want to dump certain clones.

-type: to specify dumping via transcript type or gene type: eg, -type gene or -type trans


=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

