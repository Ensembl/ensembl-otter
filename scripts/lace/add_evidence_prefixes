#!/usr/local/bin/perl -w

### add_evidence_prefixes

use strict;
use Bio::Otter::Lace::Defaults;

{
    my( $dataset_name );

    my $usage = sub { exec('perldoc', $0) };
    Bio::Otter::Lace::Defaults::do_getopt(
        'h|help!'       => $usage,
        'dataset=s'     => \$dataset_name,
        ) or $usage->();
    $usage->() unless $dataset_name;
    
    # Client communicates with otter HTTP server
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    
    # DataSet interacts directly with an otter database
    my $ds = $cl->get_DataSet_by_name($dataset_name);
    
    my $otter_dba = $ds->get_cached_DBAdaptor;
    my $pipe_dba = Bio::Otter::Lace::PipelineDB::get_pipeline_DBAdaptor($otter_dba);
    
    my $list_missing_pre = $otter_dba->prepare(q{
        SELECT evidence_id
          , evidence_name
          , type
        FROM evidence
        WHERE evidence_name NOT like '%:%'
        });
    
    my $get_db = $pipe_dba->prepare(q{
        SELECT hit_db
        FROM hit_description
        WHERE hit_name = ?
        });
    
    my $update_pre = $otter_dba->prepare(q{
        UPDATE evidence
        SET evidence_name = ?
        WHERE evidence_id = ?
    });
    
    $list_missing_pre->execute;
    while (my ($id, $name, $type) = $list_missing_pre->fetchrow) {
        my $prefix;
        if ($type eq 'EST' or $type eq 'cDNA') {
            $prefix = 'Em';
        } else {
            $get_db->execute($name);
            if (my ($db) = $get_db->fetchrow) {
                if ($db eq 'Swissprot') {
                    $prefix = 'Sw';
                }
                elsif ($db eq 'TrEMBL') {
                    $prefix = 'Tr';
                }
                else {
                    warn "Unknown database '$db'";
                    next;
                }
            }
        }
        if ($prefix) {
            #print STDERR "$type $prefix:$name\n";
            $update_pre->execute("$prefix:$name", $id);
        } else {
            print STDERR "NO PREFIX: $type $name\n";
        }
    }
}





__END__

=head1 NAME - add_evidence_prefixes

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

