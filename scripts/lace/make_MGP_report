#!/usr/bin/env perl
# Copyright [2018] EMBL-European Bioinformatics Institute
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#      http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use warnings;


### make_MGP_report

use strict;
use Bio::Otter::Lace::Defaults;

{
    my $dataset_name = 'mouse';

    my $usage = sub { exec('perldoc', $0) };
    # This do_getopt() call is needed to parse the otter config files
    # even if you aren't giving any arguments on the command line.
    Bio::Otter::Lace::Defaults::do_getopt(
        'h|help!'       => $usage,
        'dataset=s'     => \$dataset_name,
        ) or $usage->();
    $usage->() unless $dataset_name;

    my $xl_table = get_xl_table(\*ARGV);
    
    # Client communicates with otter HTTP server
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    
    # DataSet interacts directly with an otter database
    my $ds = $cl->get_DataSet_by_name($dataset_name);
    
    my $otter_dba = $ds->get_cached_DBAdaptor;
    my $gene_aptr = $otter_dba->get_GeneAdaptor;
    
    my $remark = 'Annotation_remark- MGP';
    
    my $list_genes = $otter_dba->prepare(q{
        SELECT s.stable_id
          , n.name
          , g.type
          , g.gene_id
        FROM gene_remark k
          , gene_info i
          , gene_name n
          , gene_stable_id s
          , gene g
        WHERE k.gene_info_id = i.gene_info_id
          AND i.gene_info_id = n.gene_info_id
          AND i.gene_stable_id = s.stable_id
          AND s.gene_id = g.gene_id
          AND k.remark = ?
        ORDER BY g.gene_id ASC
        });
    $list_genes->execute($remark);
    
    my %stable_gene_info;
    while (my ($stable, $name, $type, $gene_id) = $list_genes->fetchrow) {
        $stable_gene_info{$stable} = [$name, $type, $gene_id];
    }
    
    my @not_in_xl;
    foreach my $stable (sort keys %stable_gene_info) {
        my ($name, $type, $gene_id) = @{$stable_gene_info{$stable}};
        
        my $gene = $gene_aptr->fetch_by_dbID($gene_id);

        # Find Riken gene identifiers, wherever they are hidden:
        my $rik = {};
        extract_rik($rik, $gene->description);
        foreach my $syn ($gene->gene_info->synonym) {
            extract_rik($rik, $syn->name);
        }
        foreach my $rem ($gene->gene_info->remark) {
            extract_rik($rik, $rem->remark);
        }
        foreach my $trans (@{$gene->get_all_Transcripts}) {
            foreach my $rem ($trans->transcript_info->remark) {
                extract_rik($rik, $rem->remark);
            }
        }
        
        my $rik_str = join(' ', sort values %$rik);
        $rik_str ||= '';
        
        my $line = $xl_table->{lc $name};
        unless ($line) {
            foreach my $rik (keys %$rik) {
                last if $line = $xl_table->{$rik};
            }
        }
        
        my $data = join("\t", $name, $rik_str, $type, $stable);
        if ($line) {
            push @$line, $data;
        } else {
            push @not_in_xl, $data;
        }
    }
    
    foreach my $line (sort {$a->[0] <=> $b->[0]} values %$xl_table) {
        my ($n, @info) = @$line;
        print join("\t", @info), "\r";
    }
    foreach my $line (sort @not_in_xl) {
        print "$line\r";
    }
}

sub extract_rik {
    my( $rik, $str ) = @_;
    
    while ($str =~ /(\w+rik)/ig) {
        $rik->{lc $1} = $1;
    }

    return;
}

sub get_xl_table {
    my ($fh) = @_;

    local $/ = "\r";

    my $table = {};
    while (<$fh>) {
        chomp;
        next unless /\w/;
        my ($gene_name) = (split /\t/, $_)[1];
        $gene_name ||= "row-$.";
        $table->{lc $gene_name} = [$., $_];
    }
    return $table;
}

__END__

=head1 NAME - make_MGP_report

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

